//GVB 725-779
/////////////////////////////////////
//Purchase menu sprite and callback//
/////////////////////////////////////

spriteset(spriteset_purchase_real_gvb_725, "gfx/gvb_725.png") {
	template_purchase(0, 140)
}

spriteset(spriteset_purchase_cc_gvb_725, "gfx/cc_gvb_725.png") {
	template_purchase(0, 140)
}

switch (FEAT_ROADVEHS, SELF, switch_purchase_sprite_gvb_725, param_livery) {
	1:	spriteset_purchase_cc_gvb_725;
    spriteset_purchase_real_gvb_725;
}

///////////////////
//Vehicle sprites//
///////////////////

spriteset(spriteset_real_gvb_725, "gfx/gvb_725.png") {
	template_tram_20(0, 20)
}
spriteset(spriteset_real_gvb_725_1, "gfx/gvb_725.png") {
	template_tram_12(0, 60)
}
spriteset(spriteset_real_gvb_725_2, "gfx/gvb_725.png") {
	template_tram_16(0, 100)
}

spriteset(spriteset_cc_gvb_725, "gfx/cc_gvb_725.png") {
	template_tram_20(0, 20)
}
spriteset(spriteset_cc_gvb_725_1, "gfx/cc_gvb_725.png") {
	template_tram_12(0, 60)
}
spriteset(spriteset_cc_gvb_725_2, "gfx/cc_gvb_725.png") {
	template_tram_16(0, 100)
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_real_gvb_725, position_in_consist ) {
	1: spriteset_real_gvb_725_1;
	2: spriteset_real_gvb_725_2;
	spriteset_real_gvb_725;
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_cc_gvb_725, position_in_consist ) {
	1: spriteset_cc_gvb_725_1;
	2: spriteset_cc_gvb_725_2;
	spriteset_cc_gvb_725;
}

switch (FEAT_ROADVEHS, SELF, switch_livery_gvb_725, param_livery) {
	1:	switch_spritegroup_cc_gvb_725;
    switch_spritegroup_real_gvb_725;
}

/////////////////////
//vehicle callbacks//
/////////////////////

//set number of articulated parts
switch (FEAT_ROADVEHS, SELF, switch_cbarticulated_gvb_725, extra_callback_info1) {
	0..2: return tram_gvb_725; //use same vehicle for all parts
	CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

//set length of each part
switch (FEAT_ROADVEHS, SELF, switch_length_gvb_725, position_in_consist ) {
	0: return 5;
	1: return 3;
	return 4;
}

//set capacity of each part
switch (FEAT_ROADVEHS, SELF, switch_capacity_gvb_725, position_in_consist ) {
	0: return 25;
	1: return 19;
	return 25;
}

//////////////////////
//vehicle properties//
//////////////////////

item (FEAT_ROADVEHS, tram_gvb_725) {
	property {
		name:							string(STR_NAME_GVB_725);
		introduction_date:				date(1974,1,1);
		model_life:						29;
		retire_early:					0;
		vehicle_life:					25;
		loading_speed:					25;
		cost_factor:					125;
		running_cost_factor:			71;
		speed:							53 km/h;
		power: 							284 hp;
		weight: 						30 ton;
		cargo_capacity: 				69;
		tractive_effort_coefficient: 	0.15;
		air_drag_coefficient: 			0; //0=default
		//sound_effect: 				; //not yet implemented
		//visual_effect:					visual_effect(VISUAL_EFFECT_DEFAULT, 0); 
		
		reliability_decay:				20;
		climates_available:				ALL_CLIMATES;
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_TRAM);
		refit_cost: 					0;
		running_cost_base:				RUNNING_COST_ROADVEH;
	}
	
	graphics {
		purchase: switch_purchase_sprite_gvb_725;
        additional_text: string(STR_DESC_GVB_725);
        length: switch_length_gvb_725;
        articulated_part: switch_cbarticulated_gvb_725;
        cargo_capacity: switch_capacity_gvb_725;
        purchase_cargo_capacity: return 23;
		switch_livery_gvb_725;
	}
}

//only set 2CC flag if cc-parameter activated
if (param_livery == 1) {
	item (FEAT_ROADVEHS, tram_gvb_725) {
		property {
			misc_flags:					bitmask(ROADVEH_FLAG_TRAM, ROADVEH_FLAG_2CC);
		}
	}
}

//cargo refit options
if (param_refit == 1) {
	item (FEAT_ROADVEHS, tram_gvb_725) {
		property {
			refittable_cargo_classes:	bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS);
		}
	}
}
else if (param_refit == 2) {
	item (FEAT_ROADVEHS, tram_gvb_725) {
		property {
			refittable_cargo_classes:	ALL_NORMAL_CARGO_CLASSES;
		}
	}
}
