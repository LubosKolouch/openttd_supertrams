//RET 351-386
/////////////////////////////////////
//Purchase menu sprite and callback//
/////////////////////////////////////

spriteset(spriteset_purchase_real_ret_351, "gfx/ret_351.png") {
	template_purchase(0, 140)
}

spriteset(spriteset_purchase_cc_ret_351, "gfx/cc_ret_351.png") {
	template_purchase(0, 140)
}

switch (FEAT_ROADVEHS, SELF, switch_purchase_sprite_ret_351, param_livery) {
	1:	spriteset_purchase_cc_ret_351;
    spriteset_purchase_real_ret_351;
}

///////////////////
//Vehicle sprites//
///////////////////

spriteset(spriteset_real_ret_351, "gfx/ret_351.png") {
	template_tram_20(0, 20)
}
spriteset(spriteset_real_ret_351_1, "gfx/ret_351.png") {
	template_tram_12(0, 60)
}
spriteset(spriteset_real_ret_351_2, "gfx/ret_351.png") {
	template_tram_20(0, 100)
}

spriteset(spriteset_cc_ret_351, "gfx/cc_ret_351.png") {
	template_tram_20(0, 20)
}
spriteset(spriteset_cc_ret_351_1, "gfx/cc_ret_351.png") {
	template_tram_12(0, 60)
}
spriteset(spriteset_cc_ret_351_2, "gfx/cc_ret_351.png") {
	template_tram_20(0, 100)
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_real_ret_351, position_in_consist ) {
	1: spriteset_real_ret_351_1;
	2: spriteset_real_ret_351_2;
    spriteset_real_ret_351;
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_cc_ret_351, position_in_consist ) {
	1: spriteset_cc_ret_351_1;
	2: spriteset_cc_ret_351_2;
    spriteset_cc_ret_351;
}

switch (FEAT_ROADVEHS, SELF, switch_livery_ret_351, param_livery) {
	1:	switch_spritegroup_cc_ret_351;
    switch_spritegroup_real_ret_351;
}

/////////////////////
//vehicle callbacks//
/////////////////////

//set number of articulated parts
switch (FEAT_ROADVEHS, SELF, switch_cbarticulated_ret_351, extra_callback_info1) {
	0..2: return tram_ret_351; //use same vehicle for all parts
	CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

//set length of each part
switch (FEAT_ROADVEHS, SELF, switch_length_ret_351, position_in_consist ) {
	0: return 5;
	1: return 3;
	return 5;
}

//set capacity of each part
switch (FEAT_ROADVEHS, SELF, switch_capacity_ret_351, position_in_consist ) {
	0: return 200;
	1: return 200;
	return 200;
}

//////////////////////
//vehicle properties//
//////////////////////

item (FEAT_ROADVEHS, tram_ret_351) {
	property {
		name:							string(STR_NAME_RET_351);
		introduction_date:				date(1964,1,1);
		model_life:						VEHICLE_NEVER_EXPIRES;
		retire_early:					0;
		vehicle_life:					25;
		loading_speed:					25;
		cost_factor:					115;
		running_cost_factor:			7;
		speed:							511 km/h;
		power: 							2250 hp;
		weight: 						26 ton;
		cargo_capacity: 				255;
		tractive_effort_coefficient: 	0.15;
		air_drag_coefficient: 			0; //0=default
		//sound_effect: 				; //not yet implemented
		//visual_effect:					visual_effect(VISUAL_EFFECT_DEFAULT, 0); 
		
		reliability_decay:				20;
		climates_available:				ALL_CLIMATES;
		refittable_cargo_classes:		ALL_NORMAL_CARGO_CLASSES;
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_TRAM);
		refit_cost: 					0;
		running_cost_base:				RUNNING_COST_ROADVEH;
	}
	
	graphics {
		purchase: switch_purchase_sprite_ret_351;
        additional_text: string(STR_DESC_RET_351);
        length: switch_length_ret_351;
        articulated_part: switch_cbarticulated_ret_351;
        cargo_capacity: switch_capacity_ret_351;
        purchase_cargo_capacity: return 24;
		switch_livery_ret_351;
	}
}

//only set 2CC flag if cc-parameter activated
if (param_livery == 1) {
	item (FEAT_ROADVEHS, tram_ret_351) {
		property {
			misc_flags:					bitmask(ROADVEH_FLAG_TRAM, ROADVEH_FLAG_2CC);
		}
	}
}

//cargo refit options
if (param_refit == 1) {
	item (FEAT_ROADVEHS, tram_ret_351) {
		property {
			refittable_cargo_classes:	bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS);
		}
	}
}
else if (param_refit == 2) {
	item (FEAT_ROADVEHS, tram_ret_351) {
		property {
			refittable_cargo_classes:	ALL_NORMAL_CARGO_CLASSES;
		}
	}
}
