//GVB 817-841
/////////////////////////////////////
//Purchase menu sprite and callback//
/////////////////////////////////////

spriteset(spriteset_purchase_real_gvb_817, "gfx/gvb_817.png") {
	template_purchase(0, 140)
}

spriteset(spriteset_purchase_cc_gvb_817, "gfx/cc_gvb_817.png") {
	template_purchase(0, 140)
}

switch (FEAT_ROADVEHS, SELF, switch_purchase_sprite_gvb_817, param_livery) {
	1:	spriteset_purchase_cc_gvb_817;
	spriteset_purchase_real_gvb_817;
}

///////////////////
//Vehicle sprites//
///////////////////

spriteset(spriteset_real_gvb_817, "gfx/gvb_817.png") {
	template_tram_16(0, 20)
}
spriteset(spriteset_real_gvb_817_1, "gfx/gvb_817.png") {
	template_tram_16(0, 60)
}
spriteset(spriteset_real_gvb_817_2, "gfx/gvb_817.png") {
	template_tram_16(0, 100)
}

spriteset(spriteset_cc_gvb_817, "gfx/cc_gvb_817.png") {
	template_tram_16(0, 20)
}
spriteset(spriteset_cc_gvb_817_1, "gfx/cc_gvb_817.png") {
	template_tram_16(0, 60)
}
spriteset(spriteset_cc_gvb_817_2, "gfx/cc_gvb_817.png") {
	template_tram_16(0, 100)
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_real_gvb_817, position_in_consist ) {
	1: spriteset_real_gvb_817_1;
	2: spriteset_real_gvb_817_2;
	spriteset_real_gvb_817;
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_cc_gvb_817, position_in_consist ) {
	1: spriteset_cc_gvb_817_1;
	2: spriteset_cc_gvb_817_2;
	spriteset_cc_gvb_817;
}

switch (FEAT_ROADVEHS, SELF, switch_livery_gvb_817, param_livery) {
	1:	switch_spritegroup_cc_gvb_817;
	switch_spritegroup_real_gvb_817;
}

/////////////////////
//vehicle callbacks//
/////////////////////

//set number of articulated parts
switch (FEAT_ROADVEHS, SELF, switch_cbarticulated_gvb_817, extra_callback_info1) {
	0..100: return tram_gvb_817; //use same vehicle for all parts
	CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

//set length of each part
switch (FEAT_ROADVEHS, SELF, switch_length_cbarticulated_gvb_817, position_in_consist ) {
        0: return 1;
        return 1;
}

//set capacity of each part
switch (FEAT_ROADVEHS, SELF, switch_capacity_cbarticulated_gvb_817, position_in_consist ) {
    return 255;
}

//////////////////////
//vehicle properties//
//////////////////////

item (FEAT_ROADVEHS, tram_gvb_817) {
	property {
		name:							string(STR_NAME_GVB_817);
		introduction_date:				date(1990,1,1);
		model_life: 					VEHICLE_NEVER_EXPIRES;
		retire_early:					0;
		vehicle_life:					22;
		loading_speed:					40;
		cost_factor:					205;
		running_cost_factor:			11;
		speed:							511 km/h;
		power: 							2535 hp;
		weight: 						51 ton;
		cargo_capacity: 				255;
		tractive_effort_coefficient: 	0.3;
		air_drag_coefficient: 			0; //0=default
		//sound_effect: 				; //not yet implemented
		//visual_effect:					visual_effect(VISUAL_EFFECT_DEFAULT, 0); 
		
		reliability_decay:				20;
		climates_available:				ALL_CLIMATES;
		refittable_cargo_classes:		ALL_NORMAL_CARGO_CLASSES;
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_TRAM);
		refit_cost: 					0;
		running_cost_base:				RUNNING_COST_ROADVEH;
	}
	
	graphics {
		purchase: switch_purchase_sprite_gvb_817;
        additional_text: string(STR_DESC_GVB_817);
        length: switch_length_cbarticulated_gvb_817;
        articulated_part: switch_cbarticulated_gvb_817;
        cargo_capacity: switch_capacity_cbarticulated_gvb_817;
		switch_livery_gvb_817;
	}
}

//only set 2CC flag if cc-parameter activated
if (param_livery == 1) {
	item (FEAT_ROADVEHS, tram_gvb_817) {
		property {
			misc_flags:					bitmask(ROADVEH_FLAG_TRAM, ROADVEH_FLAG_2CC);
		}
	}
}

//cargo refit options
if (param_refit == 1) {
	item (FEAT_ROADVEHS, tram_gvb_817) {
		property {
			refittable_cargo_classes:	bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS);
		}
	}
}
else if (param_refit == 2) {
	item (FEAT_ROADVEHS, tram_gvb_817) {
		property {
			refittable_cargo_classes:	ALL_NORMAL_CARGO_CLASSES;
		}
	}
}
