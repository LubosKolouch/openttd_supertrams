//HTM 1301_2101-1340 + trailer 2101-2130
/////////////////////////////////////
//Purchase menu sprite and callback//
/////////////////////////////////////

spriteset(spriteset_purchase_real_htm_1301_2101, "gfx/htm_2101.png") {
	template_purchase(0, 60)
}

spriteset(spriteset_purchase_cc_htm_1301_2101, "gfx/cc_htm_2101.png") {
	template_purchase(0, 60)
}

switch (FEAT_ROADVEHS, SELF, switch_purchase_sprite_htm_1301_2101, param_livery) {
	1:	spriteset_purchase_cc_htm_1301_2101;
    spriteset_purchase_real_htm_1301_2101;
}

///////////////////
//Vehicle sprites//
///////////////////

spriteset(spriteset_real_htm_2101, "gfx/htm_2101.png") {
	template_tram_32(0, 20)
}

spriteset(spriteset_cc_htm_2101, "gfx/cc_htm_2101.png") {
	template_tram_32(0, 20)
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_real_htm_1301_2101, position_in_consist ) {
	1: spriteset_real_htm_2101;
    spriteset_real_htm_1301;
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_cc_htm_1301_2101, position_in_consist ) {
	1: spriteset_cc_htm_2101;
    spriteset_cc_htm_1301;
}

switch (FEAT_ROADVEHS, SELF, switch_livery_htm_1301_2101, param_livery) {
	1:	switch_spritegroup_cc_htm_1301_2101;
    switch_spritegroup_real_htm_1301_2101;
}

/////////////////////
//vehicle callbacks//
/////////////////////

//set number of articulated parts
switch (FEAT_ROADVEHS, SELF, switch_cbarticulated_htm_1301_2101, extra_callback_info1) {
	0..1: return tram_htm_1301_2101; //use same vehicle for all parts
	CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

//set capacity of each part
switch (FEAT_ROADVEHS, SELF, switch_capacity_htm_1301_2101, position_in_consist ) {
	0: return 38;
	return 36;
}

//////////////////////
//vehicle properties//
//////////////////////

item (FEAT_ROADVEHS, tram_htm_1301_2101) {
	property {
		name:							string(STR_NAME_HTM_1301_2101);
		introduction_date:				date(1971,1,1);
		model_life:						22;
		retire_early:					0;
		vehicle_life:					22;
		loading_speed:					30;
		cost_factor:					143;
		running_cost_factor:			84;
		speed:							72 km/h;
		power: 							483 hp;
		weight: 						32 ton;
		cargo_capacity: 				74;
		tractive_effort_coefficient: 	0.3;
		air_drag_coefficient: 			0; //0=default
		//sound_effect: 				; //not yet implemented
		//visual_effect:					visual_effect(VISUAL_EFFECT_DEFAULT, 0); 
		
		reliability_decay:				20;
		climates_available:				ALL_CLIMATES;
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_TRAM);
		refit_cost: 					0;
		running_cost_base:				RUNNING_COST_ROADVEH;
	}
	
	graphics {
		purchase: switch_purchase_sprite_htm_1301_2101;
        additional_text: string(STR_DESC_HTM_1301_2101);
        articulated_part: switch_cbarticulated_htm_1301_2101;
        cargo_capacity: switch_capacity_htm_1301_2101;
        purchase_cargo_capacity: return 37;
		switch_livery_htm_1301_2101;
	}
}

//only set 2CC flag if cc-parameter activated
if (param_livery == 1) {
	item (FEAT_ROADVEHS, tram_htm_1301_2101) {
		property {
			misc_flags:					bitmask(ROADVEH_FLAG_TRAM, ROADVEH_FLAG_2CC);
		}
	}
}

//cargo refit options
if (param_refit == 1) {
	item (FEAT_ROADVEHS, tram_htm_1301_2101) {
		property {
			refittable_cargo_classes:	bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS);
		}
	}
}
else if (param_refit == 2) {
	item (FEAT_ROADVEHS, tram_htm_1301_2101) {
		property {
			refittable_cargo_classes:	ALL_NORMAL_CARGO_CLASSES;
		}
	}
}
