//Connexxion 5001

/////////////////////////////////////
//Purchase menu sprite and callback//
/////////////////////////////////////

spriteset(spriteset_purchase_real_connexxion_5001, "gfx/westnederland_5001.png") {
	template_purchase(0, 100)
}

spriteset(spriteset_purchase_real_connexxion_5002, "gfx/connexxion_5001.png") {
	template_purchase(0, 100)
}

spriteset(spriteset_purchase_real_connexxion_5003, "gfx/bru_5001.png") {
	template_purchase(0, 100)
}

spriteset(spriteset_purchase_cc_connexxion_5001, "gfx/cc_connexxion_5001.png") {
	template_purchase(0, 100)
}

switch (FEAT_ROADVEHS, SELF, spriteset_purchase_real_connexxion_5001_date, current_date) {
	0..date(2000,12,31): spriteset_purchase_real_connexxion_5001;
    date(2001,01,01)..date(2011,12,31): spriteset_purchase_real_connexxion_5002;
    spriteset_purchase_real_connexxion_5003;
}

switch (FEAT_ROADVEHS, SELF, switch_purchase_sprite_connexxion_5001, param_livery) {
	1:	spriteset_purchase_cc_connexxion_5001;
    spriteset_purchase_real_connexxion_5001_date;
}

///////////////////
//Vehicle sprites//
///////////////////

spriteset(spriteset_real_connexxion_5001, "gfx/westnederland_5001.png") {
	template_tram_28(0, 20)
}
spriteset(spriteset_real_connexxion_5001_1, "gfx/westnederland_5001.png") {
	template_tram_28(0, 60)
}

spriteset(spriteset_real_connexxion_5002, "gfx/connexxion_5001.png") {
	template_tram_28(0, 20)
}
spriteset(spriteset_real_connexxion_5002_1, "gfx/connexxion_5001.png") {
	template_tram_28(0, 60)
}

spriteset(spriteset_real_connexxion_5003, "gfx/bru_5001.png") {
	template_tram_28(0, 20)
}
spriteset(spriteset_real_connexxion_5003_1, "gfx/bru_5001.png") {
	template_tram_28(0, 60)
}

spriteset(spriteset_cc_connexxion_5001, "gfx/cc_connexxion_5001.png") {
	template_tram_28(0, 20)
}
spriteset(spriteset_cc_connexxion_5001_1, "gfx/cc_connexxion_5001.png") {
	template_tram_28(0, 60)
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_real_connexxion_5001, position_in_consist ) {
	1: spriteset_real_connexxion_5001_1;
    spriteset_real_connexxion_5001;
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_real_connexxion_5002, position_in_consist ) {
	1: spriteset_real_connexxion_5002_1;
    spriteset_real_connexxion_5002;
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_real_connexxion_5003, position_in_consist ) {
	1: spriteset_real_connexxion_5003_1;
    spriteset_real_connexxion_5003;
}

switch (FEAT_ROADVEHS, PARENT, switch_spritegroup_real_connexxion_5001_date, date_of_last_service) {
	0..date(2000,12,31): switch_spritegroup_real_connexxion_5001;
    date(2001,01,01)..date(2011,12,31): switch_spritegroup_real_connexxion_5002;
    switch_spritegroup_real_connexxion_5003;
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_cc_connexxion_5001, position_in_consist ) {
	1: spriteset_cc_connexxion_5001_1;
    spriteset_cc_connexxion_5001;
}

switch (FEAT_ROADVEHS, SELF, switch_livery_connexxion_5001, param_livery) {
	1:	switch_spritegroup_cc_connexxion_5001;
    switch_spritegroup_real_connexxion_5001_date;
}

/////////////////////
//vehicle callbacks//
/////////////////////

//set number of articulated parts
switch (FEAT_ROADVEHS, SELF, switch_cbarticulated_connexxion_5001, extra_callback_info1) {
	0..1: return tram_connexxion_5001; //use same vehicle for all parts
	CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

//////////////////////
//vehicle properties//
//////////////////////

item (FEAT_ROADVEHS, tram_connexxion_5001) {
	property {
		name:							string(STR_NAME_CONNEXXION_5001);
		introduction_date:				date(1983,1,1);
		model_life:						VEHICLE_NEVER_EXPIRES;
		retire_early:					0;
		vehicle_life:					25;
		loading_speed:					20;
		cost_factor:					166;
		running_cost_factor:			95;
		speed:							81 km/h;
		power: 							671 hp;
		weight: 						38 ton;
		cargo_capacity: 				84;
		tractive_effort_coefficient: 	0.1;
		air_drag_coefficient: 			0; //0=default
		//sound_effect: 				; //not yet implemented
		//visual_effect:					visual_effect(VISUAL_EFFECT_DEFAULT, 0); 
		
		reliability_decay:				20;
		climates_available:				ALL_CLIMATES;
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_TRAM);
		refit_cost: 					0;
		running_cost_base:				RUNNING_COST_ROADVEH;
	}
	
	graphics {
		purchase: switch_purchase_sprite_connexxion_5001;
        additional_text: string(STR_DESC_CONNEXXION_5001);
        length: return 7;
        articulated_part: switch_cbarticulated_connexxion_5001;
        cargo_capacity: return 42;
		switch_livery_connexxion_5001;
	}
}

//only set 2CC flag if cc-parameter activated
if (param_livery == 1) {
	item (FEAT_ROADVEHS, tram_connexxion_5001) {
		property {
			misc_flags:					bitmask(ROADVEH_FLAG_TRAM, ROADVEH_FLAG_2CC);
		}
	}
}

//cargo refit options
if (param_refit == 1) {
	item (FEAT_ROADVEHS, tram_connexxion_5001) {
		property {
			refittable_cargo_classes:	bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS);
		}
	}
}
else if (param_refit == 2) {
	item (FEAT_ROADVEHS, tram_connexxion_5001) {
		property {
			refittable_cargo_classes:	ALL_NORMAL_CARGO_CLASSES;
		}
	}
}
