//HTM 201-216 + trailer 751-781
/////////////////////////////////////
//Purchase menu sprite and callback//
/////////////////////////////////////

spriteset(spriteset_purchase_real_htm_201_780, "gfx/htm_780.png") {
	template_purchase(0, 60)
}

spriteset(spriteset_purchase_cc_htm_201_780, "gfx/cc_htm_780.png") {
	template_purchase(0, 60)
}

switch (FEAT_ROADVEHS, SELF, switch_purchase_sprite_htm_201_780, param_livery) {
	1:	spriteset_purchase_cc_htm_201_780;
    spriteset_purchase_real_htm_201_780;
}

///////////////////
//Vehicle sprites//
///////////////////

spriteset(spriteset_real_htm_780, "gfx/htm_780.png") {
	template_tram_28(0, 20)
}
spriteset(spriteset_cc_htm_780, "gfx/cc_htm_780.png") {
	template_tram_28(0, 20)
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_real_htm_201_780, position_in_consist ) {
	1: spriteset_real_htm_780;
    spriteset_real_htm_201;
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_cc_htm_201_780, position_in_consist ) {
	1: spriteset_cc_htm_780;
    spriteset_cc_htm_201;
}

switch (FEAT_ROADVEHS, SELF, switch_livery_htm_201_780, param_livery) {
	1:	switch_spritegroup_cc_htm_201_780;
    switch_spritegroup_real_htm_201_780;
}

/////////////////////
//vehicle callbacks//
/////////////////////

//set number of articulated parts
switch (FEAT_ROADVEHS, SELF, switch_cbarticulated_htm_201_780, extra_callback_info1) {
	0..1: return tram_htm_201_780; //use same vehicle for all parts
	CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

//set capacity of each part
switch (FEAT_ROADVEHS, SELF, switch_capacity_htm_201_780, position_in_consist ) {
	0: return 37;
	return 35;
}

//////////////////////
//vehicle properties//
//////////////////////

item (FEAT_ROADVEHS, tram_htm_201_780) {
	property {
		name:							string(STR_NAME_HTM_201_780);
		introduction_date:				date(1948,1,1);
		model_life:						17;
		retire_early:					0;
		vehicle_life:					17;
		loading_speed:					20;
		cost_factor:					130;
		running_cost_factor:			73;
		speed:							53 km/h;
		power: 							241 hp;
		weight: 						33 ton;
		cargo_capacity: 				72;
		tractive_effort_coefficient: 	0.2;
		air_drag_coefficient: 			0; //0=default
		//sound_effect: 				; //not yet implemented
		//visual_effect:					visual_effect(VISUAL_EFFECT_DEFAULT, 0); 
		
		reliability_decay:				20;
		climates_available:				ALL_CLIMATES;
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_TRAM);
		refit_cost: 					0;
		running_cost_base:				RUNNING_COST_ROADVEH;
	}
	
	graphics {
		purchase: switch_purchase_sprite_htm_201_780;
        additional_text: string(STR_DESC_HTM_201_780);
        length: return 7;
        articulated_part: switch_cbarticulated_htm_201_780;
        cargo_capacity: switch_capacity_htm_201_780;
        purchase_cargo_capacity: return 36;
		switch_livery_htm_201_780;
	}
}

//only set 2CC flag if cc-parameter activated
if (param_livery == 1) {
	item (FEAT_ROADVEHS, tram_htm_201_780) {
		property {
			misc_flags:					bitmask(ROADVEH_FLAG_TRAM, ROADVEH_FLAG_2CC);
		}
	}
}

//cargo refit options
if (param_refit == 1) {
	item (FEAT_ROADVEHS, tram_htm_201_780) {
		property {
			refittable_cargo_classes:	bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS);
		}
	}
}
else if (param_refit == 2) {
	item (FEAT_ROADVEHS, tram_htm_201_780) {
		property {
			refittable_cargo_classes:	ALL_NORMAL_CARGO_CLASSES;
		}
	}
}
