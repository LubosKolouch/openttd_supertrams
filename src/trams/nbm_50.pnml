//NBM 50
/////////////////////////////////////
//Purchase menu sprite and callback//
/////////////////////////////////////

spriteset(spriteset_purchase_real_nbm_50, "gfx/nbm_50.png") {
	template_purchase(0, 100)
}

spriteset(spriteset_purchase_cc_nbm_50, "gfx/cc_nbm_50.png") {
	template_purchase(0, 100)
}

switch (FEAT_ROADVEHS, SELF, switch_purchase_sprite_nbm_50, param_livery) {
	1:	spriteset_purchase_cc_nbm_50;
    spriteset_purchase_real_nbm_50;
}

///////////////////
//Vehicle sprites//
///////////////////

spriteset(spriteset_real_nbm_50, "gfx/nbm_50.png") {
	template_tram_8(0, 20)
}
spriteset(spriteset_real_nbm_50_1, "gfx/nbm_50.png") {
	template_tram_24(0, 60)
}

spriteset(spriteset_cc_nbm_50, "gfx/cc_nbm_50.png") {
	template_tram_8(0, 20)
}
spriteset(spriteset_cc_nbm_50_1, "gfx/cc_nbm_50.png") {
	template_tram_24(0, 60)
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_real_nbm_50, position_in_consist ) {
	0: spriteset_real_nbm_50;
	spriteset_real_nbm_50_1;
	
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_cc_nbm_50, position_in_consist ) {
	0: spriteset_cc_nbm_50;
	spriteset_cc_nbm_50_1;
}

switch (FEAT_ROADVEHS, SELF, switch_livery_nbm_50, param_livery) {
	1:	switch_spritegroup_cc_nbm_50;
    switch_spritegroup_real_nbm_50;
}

/////////////////////
//vehicle callbacks//
/////////////////////

//set number of articulated parts
switch (FEAT_ROADVEHS, SELF, switch_cbarticulated_nbm_50, extra_callback_info1) {
	0..3: return tram_nbm_50; //use same vehicle for all parts
	CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

//set length of each part
switch (FEAT_ROADVEHS, SELF, switch_length_nbm_50, position_in_consist ) {
	1..3: return 1;
	return 1;
}

//set capacity of each part
switch (FEAT_ROADVEHS, SELF, switch_capacity_nbm_50, position_in_consist ) {
	1..3: return 80;
	return 80;
}

//////////////////////
//vehicle properties//
//////////////////////

item (FEAT_ROADVEHS, tram_nbm_50) {
	property {
		name:							string(STR_NAME_NBM_50);
		introduction_date:				date(1940,1,1);
		model_life:						VEHICLE_NEVER_EXPIRES;
		retire_early:					0;
		vehicle_life:					250;
		loading_speed:					255;
		cost_factor:					24;
		running_cost_factor:			3;
		speed:							511 km/h;
		power: 							2535 hp;
		weight: 						48 ton;
		cargo_capacity: 				80;
		tractive_effort_coefficient: 	0.075;
		air_drag_coefficient: 			0; //0=default
		//sound_effect: 				; //not yet implemented
		//visual_effect:					visual_effect(VISUAL_EFFECT_DEFAULT, 0); 
		
		reliability_decay:				20;
		climates_available:				ALL_CLIMATES;
		refittable_cargo_classes:		ALL_NORMAL_CARGO_CLASSES;
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_TRAM);
		refit_cost: 					0;
		running_cost_base:				RUNNING_COST_ROADVEH;
	}
	
	graphics {
		purchase: switch_purchase_sprite_nbm_50;
        additional_text: string(STR_DESC_NBM_50);
        length: switch_length_nbm_50;
        articulated_part: switch_cbarticulated_nbm_50;
        cargo_capacity: switch_capacity_nbm_50;
        purchase_cargo_capacity: return 27;
		switch_livery_nbm_50;
	}
}

//only set 2CC flag if cc-parameter activated
if (param_livery == 1) {
	item (FEAT_ROADVEHS, tram_nbm_50) {
		property {
			misc_flags:					bitmask(ROADVEH_FLAG_TRAM, ROADVEH_FLAG_2CC);
		}
	}
}

//cargo refit options
if (param_refit == 1) {
	item (FEAT_ROADVEHS, tram_nbm_50) {
		property {
			refittable_cargo_classes:	bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS);
		}
	}
}
else if (param_refit == 2) {
	item (FEAT_ROADVEHS, tram_nbm_50) {
		property {
			refittable_cargo_classes:	ALL_NORMAL_CARGO_CLASSES;
		}
	}
}
