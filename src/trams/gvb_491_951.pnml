//GVB 491_951-550 + trailer 951-1000

/////////////////////////////////////
//Purchase menu sprite and callback//
/////////////////////////////////////

spriteset(spriteset_purchase_real_gvb_491_951, "gfx/gvb_951.png") {
	template_purchase(0, 60)
}

spriteset(spriteset_purchase_cc_gvb_491_951, "gfx/cc_gvb_951.png") {
	template_purchase(0, 60)
}

switch (FEAT_ROADVEHS, SELF, switch_purchase_sprite_gvb_491_951, param_livery) {
	1:	spriteset_purchase_cc_gvb_491_951;
    spriteset_purchase_real_gvb_491_951;
}

///////////////////
//Vehicle sprites//
///////////////////

spriteset(spriteset_real_gvb_951, "gfx/gvb_951.png") {
	template_tram_32(0, 20)
}

spriteset(spriteset_cc_gvb_951, "gfx/cc_gvb_951.png") {
	template_tram_32(0, 20)
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_real_gvb_491_951, position_in_consist ) {
	1: spriteset_real_gvb_951;
    spriteset_real_gvb_491;
}

switch (FEAT_ROADVEHS, SELF, switch_spritegroup_cc_gvb_491_951, position_in_consist ) {
	1: spriteset_cc_gvb_951;
    spriteset_cc_gvb_491;
}

switch (FEAT_ROADVEHS, SELF, switch_livery_gvb_491_951, param_livery) {
	1:	switch_spritegroup_cc_gvb_491_951;
    switch_spritegroup_real_gvb_491_951;
}

/////////////////////
//vehicle callbacks//
/////////////////////

//set number of articulated parts
switch (FEAT_ROADVEHS, SELF, switch_cbarticulated_gvb_491_951, extra_callback_info1) {
	0..1: return tram_gvb_491_951; //use same vehicle for all parts
	CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

//set capacity of each part
switch (FEAT_ROADVEHS, SELF, switch_capacity_gvb_491_951, position_in_consist ) {
	0: return 34;
	return 36;
}

//////////////////////
//vehicle properties//
//////////////////////

item (FEAT_ROADVEHS, tram_gvb_491_951) {
	property {
		name:							string(STR_NAME_GVB_491_951);
		introduction_date:				date(1948,1,1);
		model_life:						35;
		retire_early:					0;
		vehicle_life:					25;
		loading_speed:					30;
		cost_factor:					107;
		running_cost_factor:			70;
		speed:							53 km/h;
		power: 							134 hp;
		weight: 						26 ton;
		cargo_capacity: 				70;
		tractive_effort_coefficient: 	0.15;
		air_drag_coefficient: 			0; //0=default
		//sound_effect: 				; //not yet implemented
		//visual_effect:					visual_effect(VISUAL_EFFECT_DEFAULT, 0); 
		
		reliability_decay:				20;
		climates_available:				ALL_CLIMATES;
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_TRAM);
		refit_cost: 					0;
		running_cost_base:				RUNNING_COST_ROADVEH;
	}
	
	graphics {
		purchase: switch_purchase_sprite_gvb_491_951;
        additional_text: string(STR_DESC_GVB_491_951);
        articulated_part: switch_cbarticulated_gvb_491_951;
        cargo_capacity: switch_capacity_gvb_491_951;
        purchase_cargo_capacity: return 35;
		switch_livery_gvb_491_951;
	}
}

//only set 2CC flag if cc-parameter activated
if (param_livery == 1) {
	item (FEAT_ROADVEHS, tram_gvb_491_951) {
		property {
			misc_flags:					bitmask(ROADVEH_FLAG_TRAM, ROADVEH_FLAG_2CC);
		}
	}
}

//cargo refit options
if (param_refit == 1) {
	item (FEAT_ROADVEHS, tram_gvb_491_951) {
		property {
			refittable_cargo_classes:	bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS);
		}
	}
}
else if (param_refit == 2) {
	item (FEAT_ROADVEHS, tram_gvb_491_951) {
		property {
			refittable_cargo_classes:	ALL_NORMAL_CARGO_CLASSES;
		}
	}
}
